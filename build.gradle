plugins {
    id "java"
    id "application"
    id "org.sonarqube" version "3.3"
    id "jacoco"
}

repositories{
    mavenLocal()
    mavenCentral()
}

sourceSets { // settings for change default folder on classpaths ( src/main/java, srs/main/resources and src/test/java)
  main.java.srcDirs = ["src"]
  main.resources.srcDirs = ["resource"]
  test.java.srcDirs = ["test"]
}

application{ // settings for 'gradle run'
    mainClass = project.mainClassName
}

dependencies{ // settings for all external depencies

    def versionJunit = "5.8.1"
    def versionLog4j = "2.14.1"

    implementation "org.apache.logging.log4j:log4j-api:${versionLog4j}"
    implementation "org.apache.logging.log4j:log4j-core:${versionLog4j}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${versionJunit}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${versionJunit}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versionJunit}"
}

jar { // settings for create fat-jar with all dependencies in it
    manifest {
        attributes "Main-Class": "${mainClassName}"
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE 
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}



test { // settings for JUNIT 5
    testLogging.events = ["passed", "skipped", "failed"]
    useJUnitPlatform()
}

jacocoTestReport { // settings for report jacoco to be used on sonar
    dependsOn test
    reports {
        xml.enabled true
    }
}

tasks['sonarqube'].dependsOn jacocoTestReport

